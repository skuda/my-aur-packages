# $Id: PKGBUILD 135252 2011-08-11 16:55:55Z miguel angel $
# Maintainer: Miguel Rasero <info@linos.es>

pkgname=('pyqt-mobility')
pkgver=1.0.1
pkgrel=4
arch=('i686' 'x86_64')
url="http://riverbankcomputing.co.uk/software/pyqtmobility/intro"
license=('GPL')
makedepends=('qt4' 'python-sip' 'dbus-python' 'python2-sip' 'phonon'
             'python-opengl' 'qt-assistant-compat' 'qtmobility')
source=("http://riverbankcomputing.co.uk/static/Downloads/PyQtMobility/PyQtMobility-gpl-${pkgver}.tar.gz")
md5sums=('fbec98d04f6256b76f81a067bcaf4bc1')

build() {
  cd "${srcdir}"

  cd "${srcdir}/PyQtMobility-gpl-${pkgver}"
  python configure.py \
    -e QtContacts \
    -e QtLocation \
    -e QtMultimediaKit \
    -e QtPublishSubscribe \
    -e QtSensors \
    -e QtServiceFramework \
    -e QtSystemInfo \
    -e QtVersit \
    -n /usr/include
 
  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package_pyqt-mobility(){
  pkgdesc="A set of Python bindings for the Qt Mobility toolkit"
  depends=('qt4' 'python-sip' 'pyqt' 'qtmobility')
  
  cd "${srcdir}/PyQtMobility-gpl-${pkgver}"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}
